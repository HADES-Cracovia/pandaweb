#!/usr/bin/perl

# Debugging
   use warnings;
   use strict;

use lib "./";
use lib "./include";

# Trb/Cts IO
 #  use TrbSim;    included in connectToCTS if required
#   use TrbNet;    #included in connectToCTS if required
   use Cts;
   use CtsConfig;
   use CtsCommands;
   
# Misc
   use POSIX qw[ceil];
   use Scalar::Util qw[looks_like_number];
   use List::Util qw[min max sum];   
   use Date::Format;
   use Data::Dumper;
   
   use Time::HiRes qw(usleep gettimeofday tv_interval);
   
   BEGIN {
      if (eval "require JSON::PP;") {
         *JSON_BIND:: = *JSON::PP::;
      } else {
         eval "require JSON;";
         *JSON_BIND:: = *JSON::XS::;
      }
   }

   our $endpoint;
   use Carp;
   $SIG{ __DIE__ } = sub { 
      my $error = $_[0];

      if ($error =~ /unknown address/ and $error =~ /trb_register_read/ and $error =~ /0xa[012]/) {
         printf("Endpoint (0x%04x) responded, but could not read CTS registers. Does the endpoint contain a CTS?\n" . 
         "If the CTS uses a different address, change the default endpoint address in\n" . 
         "htdocs/CtsConfig.pm, or use the --endpoint parameter when starting the CTS tool.", $endpoint);
         
      } elsif ($error =~ /no endpoint has been reached/) {
         printf("Endpoint (0x%04x) not reached. Please ensure that \n" . 
         "(1) the correct DAQOPSERVER is used and\n" .
         "(2) the CTS is configured to this address.\n" .
         "If the CTS uses a different address, change the default endpoint address in\n" . 
         "htdocs/CtsConfig.pm, or use the --endpoint parameter when starting the CTS tool.", $endpoint);
      }
      
      print "\n\n\n--------------------- More details error description ------------------------------\n";
      Carp::confess( @_ );
      
      exit;
   };   
   

   use FileHandle;
   
   use CtsPlugins::CtsModStatic;
   
   
sub help {
   print <<'END_MSG';
cts [options] command

Options:
   -h | --help      Produce this message and quit
   -e | --endpoint  Endpoint number of CTS
   -s | --sim       Simulate TrbNet
   -i | --interval  Interval of rate monitoring in milliseconds. Default: 1000
   -q | --quiet     Prevent monitor from writing values to stdout
   -l | --log       [interval,]path-to-log-file
                    Applicable only in monitoring mode, this option enables
                    logging of the trigger rates into an cvs file. Example:
                    cts -l 2,/dev/shm/cts-log m  
                    Will write a message every 3 monitoring cycles (i.e. skip 2)
   
Commands:
   l | list         Connect to CTS and list all named registers available
   
   d | dump         Read configuration from CTS and generate trbcmd-script to
                    reproduce setup after TrbNet reset
   
   r KEYS    |      Read named registers from CTS. KEYS is a white-space 
   read KEYS        seperated list of registes available (see command l)
                    Example: cts.pl r trg_pulser_config0 trg_coin_config1
                     
   w VALUES     |   Write value to CTS's named registers. VALUES is a comma 
   write VALUES     sepearated list in the following format: {key}={value}
                    Example: cts.pl w reg0=0x12, trg_coin_config0.window=10
                     
   m [dir] |        Rate Monitor. Periodically fetch counters and calculate 
   monitor [dir]    rates. Results are send to STDOUT in an human readable form.
                    If [dir] is provided the data is additionally dumped into
                    in the JSON format. Further a trigger rate plot
                    is stored in [file].png

END_MSG
}



sub connectToCTS {
   my $endpoint = shift;
   
   die("Please set the enviroment variable DAQOPSERVER") unless defined $ENV{'DAQOPSERVER'};
     
   my $trb;
   eval {require "TrbNet.pm"};
   $trb = TrbNet->new($endpoint);

   return Cts->new($trb);
}

####################################################################################
$endpoint = CtsConfig->getDefaultEndpoint;

my $updateInterval = 1000;
my $quiet = 0;
my $logPath = "";
my $logSkip = 1;

for(my $i=0; $i < @ARGV; $i++) {
   my $arg = $ARGV[$i];
   chomp $arg;
   $arg = lc $arg;
   
   if ($arg eq "-h" or $arg eq "--help") {
      help();
      exit();
      
   } elsif ($arg eq "-e" or $arg eq "--endpoint") {
      unless ($i < @ARGV) {
         print "last parameter expects value\n";
         exit();
      }
      
      $i++;
      
      if ($ARGV[$i] =~ /0x([\da-f]{1,4})/) {
         $endpoint = hex($1);
      } else {
         print "endpoint requires hex-number\n";
         exit();
      }
   
   } elsif ($arg eq "-i" or $arg eq "--interval") {
      unless ($i < @ARGV) {
         print "last parameter expects value\n";
         exit();
      }
      
      $updateInterval = $ARGV[++$i];

   } elsif ($arg eq "-q" or $arg eq "--quiet") {
      unless ($i < @ARGV) {
         print "last parameter expects value\n";
         exit();
      }
      
      $quiet = 1;
      
   } elsif ($arg eq "-n") {
      print "Support for the -n Option discontinued";
      
   } elsif ($arg eq "-l" or $arg eq "--log") {
      unless ($i < @ARGV) {
         print "last parameter expects value\n";
         exit();
      }
      
      my @tmp = split /,/, $ARGV[++$i], 2;
      $logPath = pop @tmp;
      $logSkip = pop @tmp;
      $logSkip = 0 if (not defined $logSkip or $logSkip < 0);
   
   } elsif ($arg eq "l" or $arg eq "list") {
      printTable commandList connectToCTS($endpoint);
      exit();
      
   } elsif ($arg eq "d" or $arg eq "dump") {
      print commandDump connectToCTS($endpoint);
      exit();
   
   } elsif ($arg eq "r" or $arg eq "read") {
      unless ($i < $#ARGV) {
         print "register name missing\n";
         exit();
      }
      
      my @list  = @ARGV[$i+1 .. $#ARGV];
      printTable commandRead(connectToCTS($endpoint), \@list);
      exit();
   
   } elsif ($arg eq "w" or $arg eq "write") {
      unless ($i < $#ARGV) {
         print "register name missing\n";
         exit();
      }
      
      my $cts = connectToCTS($endpoint);
      commandWrite($cts, lc join(" ", @ARGV[$i+1 .. $#ARGV]));
      exit();
   
   } elsif ($arg eq "m" or $arg eq "monitor") {
      my $cts = connectToCTS($endpoint);
      commandMonitor($cts, {
         'dump_dir' => $ARGV[++$i],
         'interval' => $updateInterval,
         'quiet'    => $quiet,
         'log_path' => $logPath,
         'log_skip' => $logSkip});
      exit();
      
   } else {
      print "Invalid argument: $arg \n";
      help();
      exit;
      
   }
}

print "Command missing\n";
help();
exit;
